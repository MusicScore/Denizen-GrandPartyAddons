###############################################################################
#                                                                             #
#                            FEVERDEATH: COMMANDS                             #
#                                                                             #
###############################################################################
# 
# For a detailed description of FeverDeath, please check out the readme.txt
# that should have come with the complete set of FeverDeath script files.
# 
# 
# ----| ABOUT THIS SCRIPT
# 
# Command scripts to add very minor in-game functionality to FeverDeath.
# 
# 
# ----| USAGE
# 
# Run these command in-game:
# 
#   + /feverdeath reload
#     - Reloads the FeverDeath configuration
#   + /feverdeath version
#     - Prints out the FeverDeath version
# 
# 


gpa_feverDeath_cmd:
  type: command
  debug: false
  name: feverdeath
  description: Access to FeverDeath<&sq>s standalone reload feature.
  usage: /feverdeath reload|version
  allowed help:
  - determine <player.is_op||<player.has_permission[gpa.feverdeath.admin]||<context.server>>>
  tab complete:
  - if !<player.has_permission[gpa.feverdeath.admin]> {
    - queue clear
  }
  - if <context.args.size> == 0 {
    - determine 'reload'
  }
  else if <context.args.size> == 1 && <el@val[reload].starts_with[<context.args.get[1]>]> {
    - determine 'reload'
  }
  script:
  - if !<server.list_scripts.parse[name].contains[gpa_feverDeath_main|gpa_feverDeath_config]> {
    - announce '[FeverDeath] WARNING: Missing the main FeverDeath script and/or the FeverDeath configuration builder script!' to_console
    - if !<context.server> {
      - narrate '[FeverDeath] WARNING: Missing the main FeverDeath script and/or the FeverDeath configuration builder script!'
    }
    - queue clear
  }
  - if <context.server> {
    - goto skip_perm_check
  }
  - if !<player.has_permission[gpa.feverdeath.admin]> {
    - define error_msg '<yaml[gpa_feverdeath_config].read[general.no permission message]||Death cannot be easily conquered...>'
    - narrate '<def[fd_prefix]><def[error_msg]>'
    - queue clear
  }
  - mark skip_perm_check
  - if <context.args.size> > 1 {
    - narrate '<&c>Too many arguments!'
    - queue clear
  }
  - choose <context.args.get[1]>:
    - case 'reload':
      - run gpa_feverDeath_config
      - narrate '<&c>Reloaded FeverDeath!'
    - case 'version':
      - narrate '<&3>GrandPartyAddon: <&b>FeverDeath v0.3.2'
    - default:
      - narrate '<&c>Invalid argument!'